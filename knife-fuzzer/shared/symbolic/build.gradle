plugins {
    id 'java-library'
    id "com.diffplug.spotless" version "6.9.0"
    id 'groovy'
}
repositories {
    mavenCentral()
}

dependencies {
    implementation libs.bundles.asm
    implementation libs.jackson.databind
    implementation libs.java.smt
    implementation rootProject.fileTree(dir: 'libs', include: ['*.jar']) // loads com.microsoft.z3

    testImplementation libs.spock.core
    testImplementation 'org.mockito:mockito-core:3.12.4'  // Adjust the version as needed

}

tasks.register('dumpDependencies') {
    doLast {
        def resolved = configurations.compileClasspath.resolvedConfiguration
        println "files=$resolved.files"
        resolved.resolvedArtifacts.each {
            artifact -> println "artifact=$artifact"
        }
    }
}

spotless {
    // optional: limit format enforcement to just the files changed by this feature branch
    //ratchetFrom 'origin/main'
    enforceCheck false

    format 'misc', {
        // define the files to apply `misc` to
        target '*.gradle', '*.md', '.gitignore','.conf'

        // define the steps to apply to those files
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat().aosp().reflowLongStrings()
    }
    groovy {
        importOrder()
        excludeJava()
        greclipse()
    }
}
test{
    maxParallelForks = Runtime.runtime.availableProcessors()
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors()

}
